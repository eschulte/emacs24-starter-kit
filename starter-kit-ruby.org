#+TITLE: Starter Kit Ruby
#+OPTIONS: toc:nil num:nil ^:nil

This is part of the [[file:starter-kit.org][Emacs Starter Kit]].

* Starter Kit Ruby

Support for the [[http://www.ruby-lang.org/en/][Ruby]] dynamic, open-source programming language.

#+begin_src emacs-lisp
(eval-after-load 'ruby-mode
  '(progn
     ;; work around possible elpa bug
     (ignore-errors (require 'ruby-compilation))
     (setq ruby-use-encoding-map nil)
     (define-key ruby-mode-map (kbd "RET") 'reindent-then-newline-and-indent)
     (define-key ruby-mode-map (kbd "C-M-h") 'backward-kill-word)
     (define-key ruby-mode-map (kbd "C-c l") "lambda")))
#+end_src

#+begin_src emacs-lisp
(global-set-key (kbd "C-h r") 'ri)
#+end_src

** Yaml mode
#+begin_src emacs-lisp
  (starter-kit-install-if-needed 'yaml-mode)
  (require 'yaml-mode)
  (add-to-list 'auto-mode-alist '("\\.ya?ml$" . yaml-mode))
#+end_src

** Rake files are ruby, too, as are gemspecs.
   :PROPERTIES:
   :CUSTOM_ID: additional-file-extensions
   :END:
#+begin_src emacs-lisp
(add-to-list 'auto-mode-alist '("\\.rake$" . ruby-mode))
(add-to-list 'auto-mode-alist '("Rakefile$" . ruby-mode))
(add-to-list 'auto-mode-alist '("\\.gemspec$" . ruby-mode))
#+end_src

** We never want to edit Rubinius bytecode
   :PROPERTIES:
   :CUSTOM_ID: ignore-rubinius
   :END:
#+begin_src emacs-lisp
(add-to-list 'completion-ignored-extensions ".rbc")
#+end_src

** Rake
   :PROPERTIES:
   :CUSTOM_ID: rake
   :END:

#+begin_src emacs-lisp
  (defun pcomplete/rake ()
    "Completion rules for the `ssh' command."
    (pcomplete-here (pcmpl-rake-tasks)))

  (defun pcmpl-rake-tasks ()
    "Return a list of all the rake tasks defined in the current
  projects.  I know this is a hack to put all the logic in the
  exec-to-string command, but it works and seems fast"
    (delq nil (mapcar (lambda (line)
                        (if (string-match "rake \\([^ ]+\\)" line) (match-string 1 line)))
                      (split-string (shell-command-to-string "rake -T") "[\n]"))))

  (defun rake (task)
    (interactive (list (completing-read "Rake (default: default): "
                                        (pcmpl-rake-tasks))))
    (shell-command-to-string (concat "rake " (if (= 0 (length task)) "default" task))))
#+end_src

** Compilation
   :PROPERTIES:
   :CUSTOM_ID: compilation
   :END:
Clear the compilation buffer between test runs.

#+begin_src emacs-lisp
(eval-after-load 'ruby-compilation
  '(progn
     (defadvice ruby-do-run-w/compilation (before kill-buffer (name cmdlist))
       (let ((comp-buffer-name (format "*%s*" name)))
         (when (get-buffer comp-buffer-name)
           (with-current-buffer comp-buffer-name
             (delete-region (point-min) (point-max))))))
     (ad-activate 'ruby-do-run-w/compilation)))
#+end_src

** Hooks
   :PROPERTIES:
   :CUSTOM_ID: hooks
   :END:
#+begin_src emacs-lisp
(add-hook 'ruby-mode-hook 'run-starter-kit-coding-hook)
#+end_src

#+begin_src emacs-lisp
;; (add-hook 'ruby-mode-hook 'idle-highlight)
#+end_src emacs-lisp

** Flymake
   :PROPERTIES:
   :CUSTOM_ID: flymake
   :END:

#+begin_src emacs-lisp
(eval-after-load 'ruby-mode
  '(progn
     (require 'flymake)

     ;; Invoke ruby with '-c' to get syntax checking
     (defun flymake-ruby-init ()
       (let* ((temp-file (flymake-init-create-temp-buffer-copy
                          'flymake-create-temp-inplace))
              (local-file (file-relative-name
                           temp-file
                           (file-name-directory buffer-file-name))))
         (list "ruby" (list "-c" local-file))))

     (push '(".+\\.rb$" flymake-ruby-init) flymake-allowed-file-name-masks)
     (push '("Rakefile$" flymake-ruby-init) flymake-allowed-file-name-masks)

     (push '("^\\(.*\\):\\([0-9]+\\): \\(.*\\)$" 1 2 nil 3)
           flymake-err-line-patterns)

     (add-hook 'ruby-mode-hook
               (lambda ()
                 (when (and buffer-file-name
                            (file-writable-p
                             (file-name-directory buffer-file-name))
                            (file-writable-p buffer-file-name))
                   (local-set-key (kbd "C-c d")
                                  'flymake-display-err-menu-for-current-line)
                   (flymake-mode t))))))
#+end_src

** Rinari (Minor Mode for Ruby On Rails)
   :PROPERTIES:
   :CUSTOM_ID: rinari
   :END:
See [[http://rinari.rubyforge.org/][rinari.rubyforge]] for more information on rinari.

#+begin_src emacs-lisp
  (setq rinari-major-modes
        '(dired-mode 'ruby-mode  'css-mode 'yaml-mode 'javascript-mode))
#+end_src
